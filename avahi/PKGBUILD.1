# $Id: PKGBUILD 172143 2012-11-30 03:01:06Z bisson $
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=avahi
pkgver=0.6.31
pkgrel=5
pkgdesc='Multicast/unicast DNS-SD framework(no gui)'
url='http://www.avahi.org/'
license=('LGPL')
arch=('i686' 'x86_64' sh4)
options=('!libtool' '!emptydirs')
depends=('expat' 'libdaemon' 'glib2' 'libcap' 'gdbm')
optdepends=('dbus: communicating with client applications'
            'twisted: avahi-bookmarks'
            'python2-dbus: avahi-discover'
            'nss-mdns: NSS support for mDNS')
makedepends=('intltool' 'python2-dbus'
             'gobject-introspection' 'xmltoman')
backup=('etc/avahi/hosts'
        'etc/avahi/avahi-daemon.conf'
        'etc/avahi/services/ssh.service'
        'etc/avahi/services/sftp-ssh.service')
source=("http://www.avahi.org/download/avahi-${pkgver}.tar.gz"
        'rc.d.patch')
sha1sums=('7e05bd78572c9088b03b1207a0ad5aba38490684'
          '625ad7c131c0c1c383caeddef18fc7a32d8f3ab9')

conflicts=('howl' 'mdnsresponder')
provides=('howl' 'mdnsresponder')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i 's/netdev/network/g' avahi-daemon/avahi-dbus.conf
	patch -p1 -i ../rc.d.patch

	# pygtk requires python2; make it explicit in case other python are installed: FS#21865
	PYTHON=python2 \
	PKG_CONFIG_PATH=/opt/qt/lib/pkgconfig \
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-mono \
		--disable-monodoc \
		--enable-compat-libdns_sd \
		--enable-compat-howl \
		--with-distro=archlinux \
		--with-avahi-priv-access-group=network \
		--with-autoipd-user=avahi \
		--with-autoipd-group=avahi \
		--disable-qt3 \
		--disable-qt4 \
		--disable-gtk \
		--disable-gtk3 \
		--disable-pygtk \
		--disable-doxygen-man \
		--disable-doxygen-rtf \
		--disable-doxygen-chm \
		--disable-doxygen-chi \
		--disable-doxygen-ps \
		--disable-doxygen-pdf \
		--disable-core-docs \

#		--with-systemdsystemunitdir=/usr/lib/systemd/system \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}"
	sed -i '1c #!/usr/bin/python2' usr/bin/avahi-bookmarks
	#sed -i '1c #!/usr/bin/python2' usr/bin/avahi-{bookmarks,discover}

	# howl and mdnsresponder compatability
	(cd usr/include; ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h; ln -s avahi-compat-howl howl)
	(cd usr/lib/pkgconfig; ln -s avahi-compat-howl.pc howl.pc)
}
