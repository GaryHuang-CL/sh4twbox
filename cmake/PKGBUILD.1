# $Id: PKGBUILD 179599 2013-03-07 17:46:37Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=cmake
pkgver=2.8.10.2
pkgrel=5
pkgdesc="A cross-platform open-source make system (without qt support)"
arch=(sh4)
url="http://www.cmake.org/"
license=('custom')
depends=('curl' 'libarchive' 'shared-mime-info')
#makedepends=('qt5-base' 'emacs')
#optdepends=('qt5-base: cmake-gui')
install="${pkgname}.install"
source=("http://www.cmake.org/files/v2.8/${pkgname}-${pkgver}.tar.gz")
md5sums=('097278785da7182ec0aea8769d06860c')

build() {
  cd ${pkgname}-${pkgver}

  # qmake refers to Qt5
  #patch -p1 -i "${srcdir}"/qt4.patch

  # Qt is built with reduce-relocations
  export CFLAGS="${CFLAGS} -fPIC"
  export CXXFLAGS="${CXXFLAGS} -fPIC"

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
 #   --qt-gui \
  # reduce build time
  sed -i 's/BUILD_TESTING:BOOL=ON/BUILD_TESTING:BOOL=OFF/' CMakeCache.txt
  # ref: http://dev.mysql.com/doc/internals/en/cmake-howto-long-version.html#listing-configuration-parameters
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm644 Docs/cmake-indent.vim "${vimpath}"/indent/cmake-indent.vim
  install -Dm644 Docs/cmake-syntax.vim "${vimpath}"/syntax/cmake-syntax.vim

  install -Dm644 Docs/cmake-mode.el \
    "${pkgdir}"/usr/share/emacs/site-lisp/cmake-mode.el
  #emacs -batch -f batch-byte-compile "${pkgdir}"/usr/share/emacs/site-lisp/cmake-mode.el

  install -Dm644 Copyright.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
